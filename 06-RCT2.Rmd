# Experimentos Controlados RECETA

::: {.boxinfo}
**üéØ Metas de aprendizaje**

- Entender c√≥mo hacer **inferencia estad√≠stica** con asignaci√≥n aleatoria.  
- Discutir **cu√°ndo** incluir controles y **qu√© ocurre** con la varianza/SE al incluirlos.  
- Traducir **resultados potenciales** a una **regresi√≥n lineal**.  
- Implementar **diagn√≥sticos de balance**, **efectos heterog√©neos** y **reporte reproducible**.

## üìö Lecturas {-}

- **Paper Alert:** [When Should You Adjust Standard Errors for Clustering? (NBER w24003)](https://www.nber.org/papers/w24003)  
- **Teor√≠a:** [Jakiela & Ozier ‚Äî Handout](http://economics.ozier.com/econ626/lec/econ626-L07-handout-2019.pdf)  
- **Herramienta:** [J-PAL ‚Äî Power calculations](https://www.povertyactionlab.org/resource/power-calculations)
:::




*1) Setup*

- Unidades: \(N\).
- Tratamiento: \(D_i \in \{0,1\}\). Asignado de manera aleatoria.
- Resultados potenciales: \(Y_i(D=1),\, Y_i(D=0)\).
- Resultado observado:
  \[
  Y_i \;=\; D_i\,Y_i(D=1) \;+\; (1-D_i)\,Y_i(D=0).
  \]
- Tama√±os de muestra:
  - Tratados: \(N_1 = \sum_i \mathbb{1}\{D_i=1\}\).
  - Control: \(N_0 = \sum_i \mathbb{1}\{D_i=0\}\).

 *2) Supuestos*

1. **Asignaci√≥n aleatoria:** cada unidad \(i\) es asignada de manera independiente al tratamiento con probabilidad \(p\).
   \[
   D_i \;\perp\; \{Y_i(1), Y_i(0)\}.
   \]
2. **SUTVA** (Stable Unit Treatment Value Assumption): no hay interferencia entre unidades ni versiones del tratamiento.  
   En espa√±ol: no hay externalidades ni efectos de equilibrio general que cambien los resultados potenciales de otros individuos.

*3) Implicaciones y par√°metros*

- **ATE (efecto promedio):**
  \[
  \tau \;=\; \mathbb{E}\!\left[\,Y(D=1)-Y(D=0)\,\right].
  \]
- **Identificaci√≥n con aleatorizaci√≥n:**
  \[
  \mathbb{E}[\,Y \mid D=1\,] - \mathbb{E}[\,Y \mid D=0\,] \;=\; \tau.
  \]
- **Estimador por diferencia de medias:**
  \[
  \hat{\tau} \;=\; \bar{Y}_{D=1}-\bar{Y}_{D=0}.
  \]
- **Estimador ‚Äúna√Øve‚Äù sin aleatorizaci√≥n (descomposici√≥n):**
  \[
  \underbrace{\mathbb{E}[Y \mid D=1] - \mathbb{E}[Y \mid D=0]}_{\text{na√Øve}}
  \;=\;
  \underbrace{\tau}_{\text{efecto causal}}
  \;+\;
  \underbrace{\mathbb{E}[Y(0)\mid D=1] - \mathbb{E}[Y(0)\mid D=0]}_{\text{sesgo de selecci√≥n}}.
  \]


## Paso a Paso para un RCT {-}

### PASO 1) Dise√±ar el experimento {-}
- Definir la **poblaci√≥n objetivo** y el **tratamiento**.
- Determinar el **tama√±o de muestra** necesario para detectar un efecto significativo (usando `power` en Stata/R).


### Paso 2) Aleatorizar la asignaci√≥n (Stata) {-}

Abajo tienes varias formas de asignar tratamiento en Stata. Incluyo opciones con `runiform()` (base) y con el comando de SSC `randtreat` (estratificado, proporciones desiguales, clusters y manejo de *misfits*).

> **Regla de oro:** fija una semilla reproducible y *gu√°rdala* en el log/notas del proyecto.
```stata
* Reproducibilidad
set seed 20250813
local RNGSTATE = c(rngstate)   // guardar estado; opcionalmente escr√≠belo en tu log
display as text "Seed guardada: `RNGSTATE'"
```

*Bernoulli simple (50/50) con `runiform()`*

Asigna tratamiento con probabilidad 0.5 a cada unidad.

```stata
* Sup√≥n que tienes un id por fila
sort id
gen double u = runiform()
gen byte D = (u < 0.5)           // 1=tratado, 0=control
tab D
drop u
```

**Cambio de proporci√≥n (p=0.30):**

```stata
gen double u = runiform()
gen byte D = (u < 0.30)
tab D
drop u
```

**Asignaci√≥n con conteo exacto de tratados (p.ej., 40% exacto)**

Asegura exactamente `round(N*0.40)` tratados.

```stata
count
local N      = r(N)
local Ntr    = round(`N'*0.40)   // n√∫mero exacto de tratados
gen double u = runiform()
sort u
gen byte D   = (_n <= `Ntr')
tab D
drop u
```

**Estratificada** (bloques) con `runiform()`

Mant√©n la proporci√≥n deseada **dentro de cada estrato** (ej., por `mujer` √ó `programa`).

> Con `round()` obtienes conteo **exacto por estrato**; globalmente puede variar levemente si los tama√±os de estrato son impares.

```stata
egen long strata = group(mujer programa), label
bys strata: gen double u = runiform()
bys strata (u): gen byte D = (_n <= round(_N*0.50))   // 50/50 exacto en cada estrato
tab D
by strata: tab D
drop u
```

**Proporci√≥n distinta por estrato (p.ej., 40%):**

```stata
bys strata: gen double u = runiform()
bys strata (u): gen byte D = (_n <= round(_N*0.40))
by strata: tab D
drop u
```

**Cluster** (aleatoriza a nivel del cluster y la pasa a individuos)

Ej.: clusters en `escuela`.

```stata
* Una fila por cluster
preserve
keep escuela
duplicates drop

gen double u = runiform()
sort u
* 50% de escuelas tratadas
count
local C = r(N)
local Ctr = round(`C'*0.50)
gen byte D_cluster = (_n <= `Ctr')
tempfile cl
save `cl', replace
restore

* Trae la asignaci√≥n al nivel individual
merge m:1 escuela using `cl', keep(match master) nogen
gen byte D = D_cluster
tab D
```

Usando **`randtreat`** (SSC): multi-brazos, proporciones desiguales, estratos y *misfits*

**Instalaci√≥n**

```stata
ssc install randtreat, replace
```

**Binario 50/50 (sin estratos)**

```stata
randtreat, generate(D) multiple(2) setseed(20250813)
tab D
```

**Binario 50/50 dentro de estratos (`mujer` √ó `programa`)**

```stata
randtreat, generate(D) multiple(2) strata(mujer programa) setseed(20250813)
by mujer programa: tab D
```

** Proporciones desiguales (30% tratado, 70% control)**

```stata
randtreat, generate(D) multiple(2) unequal(0.30 0.70) setseed(20250813)
tab D
```

** Multi-brazo (T1/T2/C = 0.4/0.4/0.2) con estratos**

```stata
randtreat, generate(arm) multiple(3) unequal(0.40 0.40 0.20) ///
    strata(mujer programa) setseed(20250813)
tab arm
by mujer programa: tab arm
```

**Manejo de *misfits***
Cuando el tama√±o de estrato no es divisible por los brazos/proporciones, usa `misfits()`:

* `misfits(strata)` o `misfits(wstrata)`: resuelve dentro de cada estrato (ponderado o no).
* `misfits(global)` o `misfits(wglobal)`: resuelve a nivel global priorizando las proporciones totales.
* `misfits(missing)`: deja *misfits* sin asignar para que los decidas manualmente.

```stata
randtreat, generate(arm) multiple(3) unequal(0.40 0.40 0.20) ///
    strata(mujer programa) misfits(wstrata) setseed(20250813)
```

**Forzar n√∫mero exacto de tratados totales** (binario)

> √ötil si necesitas exactamente `N_t` tratados (no solo proporci√≥n).

```stata
* Por ejemplo, exactamente 50 tratados en total:
randtreat, generate(D) multiple(2) ntreated(50) setseed(20250813)
tab D
```

**Cluster + `randtreat`**
Ejecuta `randtreat` sobre los **clusters √∫nicos** y luego une a individuos.

```stata
preserve
keep escuela mujer programa
duplicates drop

* 2 brazos 50/50 por estratos de escuela (y opcionalmente otras):
randtreat, generate(Dcl) multiple(2) strata(mujer programa) setseed(20250813)
tempfile cl
save `cl', replace
restore

merge m:1 escuela using `cl', nogen
gen byte D = Dcl
tab D
```

### PASO 3) Verificar el balance de covariables {-}


```stata
use "data.dta", clear

* Tratamiento 1=B, 0=A
gen D = (grupo == "B")
label define Dlbl 0 "Control" 1 "Tratado"
label values D Dlbl

* Outcome y y controles
gen y        = resultado
gen mujer    = (genero == "Mujer")
gen pregrado = (programa == "Pregrado")
gen maestria = (programa == "Maestr√≠a")

global X edad mujer libros pregrado maestria
```
* Programa de diferencia de medias con t-test y opci√≥n de guardar resultados en un archivo .dta**

1. **Pruebas univariadas (t‚Äêtests):** comparar medias de cada covariable \(X\) entre \(D=1\) y \(D=0\).  
   Objetivo: **no rechazar** que las medias son iguales.
2. **Prueba conjunta (F‚Äêtest):** regresi√≥n de \(D\) sobre \(X\) (LPM) y test conjunto de significancia de \(X\).
3. **Modelos binarios:** `logit/probit` de \(D\) sobre \(X\); evaluar raz√≥n de verosimilitud o \(\chi^2\) conjunta.
4. **Si se rechaza balance:** revisar procedimiento (bloqueos/estratos, reasignaci√≥n, control por \(X\) pretratamiento en la estimaci√≥n).

```stata
cap program drop difmedias
program define difmedias, rclass
    version 18
    syntax varlist(min=1) [, BY(varname) SAVEPOST(string asis)]
    if ("`by'"=="") {
        di as err "Necesitas especificar , by(varname)."
        exit 198
    }
    local vars `varlist'

    local do_post = 0
    if ("`savepost'"!="") local do_post = 1
    tempname posth
    if `do_post' {
        postfile `posth' str32 variable ///
            double mean_T mean_C diff tstat pval se sd_T sd_C N_T N_C ///
            using "`savepost'", replace
    }

    di as txt "Var{col 22}Mean_T{col 33}Mean_C{col 44}Diff{col 55}t{col 66}p"
    foreach v of local vars {
        qui ttest `v', by(`by')

        local mu1 = r(mu_1)
        local mu2 = r(mu_2)
        local sd1 = r(sd_1)
        local sd2 = r(sd_2)
        local N1  = r(N_1)
        local N2  = r(N_2)
        local t   = r(t)
        local se  = r(se)
        local p   = r(p)

        local mean_T = `mu2'
        local mean_C = `mu1'
        local diff   = `mu2' - `mu1'

        di as res "`v'" "{col 22}" %9.3f `mean_T' "{col 33}" %9.3f `mean_C' "{col 44}" %9.3f `diff' "{col 55}" %9.3f `t' "{col 66}" %9.3f `p'

        if `do_post' {
            post `posth' ("`v'") (`mean_T') (`mean_C') (`diff') (`t') (`p') (`se') (`sd2') (`sd1') (`N2') (`N1')
        }
    }
    if `do_post' postclose `posth'
end


* Balance de X
difmedias $X, by(D) savepost("Table_Balance_raw.dta")

use "Table_Balance_raw.dta", clear
order variable mean_T mean_C diff tstat pval se sd_T sd_C N_T N_C
export excel using "Table_Balance_raw.xlsx", firstrow(variables) replace

* ALTERNATIVAS 
cap which iebaltab
if _rc ssc install ietoolkit, replace

iebaltab $X, grpvar(D) control(0) rowvarlabels ftest ///
     savexlsx("Table_Balance.xlsx") replace format(%9.3f)

eststo clear
eststo: reg   D $X, vce(robust)
test $X
eststo: logit D $X, vce(robust)
eststo: probit D $X, vce(robust)

esttab, se star(* 0.10 ** 0.05 *** 0.01) ///
    stats(N r2, fmt(%9.0g %9.3f) labels("N" "R2")) ///
    nomtitle compress

```

### PASO 4) Estimar el efecto del tratamiento {-}

\[
Y_i \;=\; \alpha \;+\; \tau D_i \;+\; \varepsilon_i.
\]
- Bajo aleatorizaci√≥n, \(\hat{\tau}_{OLS}\) es insesgado y coincide con la diferencia de medias.

Sea \(D\) la columna binaria (0/1) y \(\iota\) el vector de unos. Entonces, en \(Y = \alpha \iota + \tau D + \varepsilon\), se tiene:
\[
\hat{\tau}
\;=\;
\bar{Y}_1 - \bar{Y}_0.
\]

```stata
eststo clear
eststo m1: reg y D, vce(robust)
eststo m2: reg y D $X, vce(robust)

esttab m1 m2, se star(* 0.10 ** 0.05 *** 0.01) ///
    stats(N r2, fmt(%9.0g %9.3f) labels("N" "R2")) ///
    b(%9.4f) compress nonote ///
    title("Efecto del tratamiento con y sin controles")
```


::: {.boxnote}
Interpretaci√≥n seg√∫n escala de \(Y\)

1. **\(Y\) en niveles (continuo):** \(\hat{\tau}\) es diferencia de unidades de \(Y\) entre tratados y control.
2. **\(Y\) en logaritmos:** \(\hat{\tau}\) es una **semi-elasticidad**; el efecto porcentual aproximado es \(100\times \hat{\tau}\%\).  
   Efecto exacto: \(100\times(\exp(\hat{\tau})-1)\%\).  
   *Ejemplo:* \(\hat{\tau}=0.13 \Rightarrow \exp(0.13)-1 \approx 0.139\) ‚Üí **13.9%**.
3. **\(Y\) estandarizado:** media 0 y varianza 1; \(\hat{\tau}\) se interpreta en **desviaciones est√°ndar**.
4. **\(Y\) binario:** \(\hat{\tau}\) es **diferencia de probabilidades** (puntos porcentuales).  
   *Ejemplo:* \(p_1=0.30\), \(p_0=0.25\) ‚áí \(\hat{\tau}=0.05 = 5\) pp.
:::




## Inferencia estad√≠stica {-}

###  Asignaci√≥n a nivel individual (dos muestras independientes) {-}
- **Varianza del estimador (cl√°sica):**
  \[
  \operatorname{Var}(\hat{\tau})
  \;\approx\;
  \frac{S_1^2}{N_1} \;+\; \frac{S_0^2}{N_0},
  \]
  donde \(S_1^2\) y \(S_0^2\) son las varianzas muestrales por grupo.
- **Heterocedasticidad:** usar **SE robustas** (HC).  
- **Asignaci√≥n por clusters:** usar **SE cluster** al nivel de asignaci√≥n. Con pocos clusters, considerar **wild cluster bootstrap**.

### Pruebas {-}
- **Significancia individual:** \(H_0:\,\tau=0\) (t‚Äêtest).  
- **Significancia conjunta de \(X\):** en \(D \sim X\), \(H_0:\,\beta_X=0\) (F/\(\chi^2\)). Queremos **no rechazar**.

## ¬øCu√°ndo incluir controles? {-}

- **Identificaci√≥n:** con aleatorizaci√≥n correcta, **no necesitas** \(X\) para identificar \(\tau\).
- **Precisi√≥n:** incluir \(X\) **pretratamiento** que **expliquen \(Y\)** puede **reducir** \(\operatorname{Var}(\hat{\tau})\) (menor varianza residual).  
  Si \(X\) es irrelevante o colineal, puede **aumentar** la SE de \(\hat{\tau}\).
- **Modelo con controles:**
  \[
  Y_i \;=\; \alpha + \tau D_i + X_i'\beta + u_i.
  \]
  Si \(X \perp D\) (por dise√±o) y \(X\) explica \(Y\), esperar **SE(D)** m√°s peque√±as.

## Efectos heterog√©neos (HET) {-}

- **Especificaci√≥n general:**
  \[
  Y_i \;=\; \alpha \;+\; \tau D_i \;+\; \gamma W_i \;+\; \delta\, (D_i \times W_i) \;+\; X_i'\beta \;+\; u_i,
  \]
  donde \(W\) es el moderador (p. ej., **mujer**, **libros**, **cuartiles de edad**).
- **Interpretaci√≥n:** \(\delta\) es el **cambio en el efecto de \(D\)** al pasar de un nivel de \(W\) a otro.  
  Usar `margins`/`marginsplot` para reportar efectos marginales y perfiles por subgrupo.

```stata
* Por mujer (binaria)
eststo clear
eststo: reg y D##i.mujer, vce(robust)
margins, dydx(D)
margins D, at(mujer=(0 1))

* Por libros (continua)
eststo: reg y D##c.libros, vce(robust)
margins, dydx(D) at(libros=(0(1)8))
marginsplot, title("Efecto marginal de D seg√∫n libros")
graph export "margins_libros.pdf", replace

* Por cuartiles de edad
xtile q_edad = edad, nq(4)
eststo: reg y D##i.q_edad, vce(robust)
margins D#q_edad
marginsplot, title("Heterogeneidad por cuartiles de edad")
graph export "margins_qedad.pdf", replace

```


::: {.boxejercicio}
**üß™ Ejercicios**

1. **Aleatorizaci√≥n (Bernoulli y estratos)**
   (a) Con tu `data.dta`, genera `D` con p=0.5 usando `runiform()`.
   (b) Repite p=0.3.
   (c) Crea estratos por `mujer √ó programa` y asigna 50/50 exacto por estrato.

2. **`randtreat` avanzado**
   (a) Asigna 3 brazos (T1/T2/C) con proporciones 0.4/0.4/0.2 y estratos `mujer √ó programa`.
   (b) Resuelve *misfits* con `misfits(wstrata)` y reporta conteos por estrato.

3. **Balance**
   (a) Corre `difmedias $X, by(D)` y exporta a `Table_Balance_raw.xlsx`.
   (b) Estima `D ~ X` (LPM/logit/probit) y reporta el test conjunto.
   (c) Interpreta: ¬øhay evidencia de desbalance?

4. **Efecto del tratamiento**
   (a) Estima $Y\sim D$ y $Y\sim D+X$ con SE robustas.
   (b) Compara $\hat\tau$ y sus SE entre modelos; discute precisi√≥n.
   (c) Si `resultado` es binario, interpreta en puntos porcentuales.

5. **Heterogeneidad**
   (a) Estima $Y\sim D\times \text{mujer}$ y reporta `margins`.
   (b) Estima $Y\sim D\times \text{libros}$, grafica `marginsplot` y comenta el perfil.

**Bonus (opcional):**
6\. Repite la estimaci√≥n con **SE cluster** suponiendo que la asignaci√≥n fue por `escuela`. Discute diferencias con SE robustas.
\
:::


## DESCARGA LOS DOCUMENTOS {-}

**Descargar Stata do file**:  
[Descargar Stata](https://raw.githubusercontent.com/adiazescobar/libro_cortes/main/dofile/Clase1_Experimentos/clase1_stata.do)

**Descargar R script**:  
[Descargar R](https://raw.githubusercontent.com/adiazescobar/libro_cortes/main/dofile/Clase1_Experimentos/clase1_R.R)

**Descargar Phyton Notebook**:  
[Descargar Phyton](https://raw.githubusercontent.com/adiazescobar/libro_cortes/main/dofile/Clase1_Experimentos/clase1_phyton.ipynb)

[![Abrir en Colab](https://colab.research.google.com/assets/colab-badge.svg)](https://colab.research.google.com/github/adiazescobar/libro_cortes/blob/main/dofile/Clase1_Experimentos/clase1_phyton.ipynb)

**Descarga los Datos**  
*(En esta clase usamos la base real `data.dta` con columnas: `id, resultado, grupo, edad, genero, programa, libros`.)*
